; dial history (jrhb)
(let* ((range (lambda (s l r) (lin-lin s 0 1 l r)))
       (mce-r (lambda (l) (make-mce (map make-mce l))))
       (mce-mrg (lambda (u) (mrg-n (mce-channels u))))
       (mfv (transpose (list (list 697 770 852 941)
                             (list 1209 1336 1477 1633))))
       (numbers (transpose (list (list 3 0 0 0 1 1 1 2 2 2)
                                 (list 1 0 1 2 0 1 2 0 1 2))))
       (n (Dwhite dinf 7 12))
       (w (Dwhite 1 2 7))
       (b (Dbrown n 0.1 0.2 0.01))
       (rt (Dseq dinf (mce2 w b)))
       (q (Dseq dinf (make-mce (enumFromTo 1 10))))
       (g1 (GrayNoise ar))
       (g2 (GrayNoise ar))
       (d (LFDNoise3 kr 0.5))
       (tr (Trig (TDuty kr rt 0 do-nothing q 1) 0.09))
       (pat (Latch tr tr))
       (x (MouseX kr 0 1 linear 0.2))
       (h (Hasher (Mul pat x)))
       (which (Trunc (range h 0 (length numbers)) 1))
       (both (Select which (mce-r numbers)))
       (dial (Select both (mce-r mfv)))
       (sig (mul3 (SinOsc ar dial 0) 0.05 tr))
       (dsig (DelayN sig 0.2 (range d 0 0.01)))
       (hiss (MulAdd g1 0.01 (HPF (Mul g2 0.02) 3000))))
  (Add dsig hiss))
