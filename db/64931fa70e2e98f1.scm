; analogue daze (jmcc) #3
(let* ((pattern (list 55 63 60 63 57 65 62 65))
       (sequ (lambda (s tr) (Demand tr 0 (Dseq dinf (make-mce s)))))
       (f (lambda (octave clockrate pwm-rate flt-rate)
            (let* ((trg (Impulse kr clockrate 0))
                   (pattern* (map (lambda (x) (MIDICPS (Add x (Mul 12 octave)))) pattern))
                   (sq (sequ pattern* trg))
                   (pwm (MulAdd (SinOsc kr pwm-rate (Rand 0 two-pi)) 0.4 0.5))
                   (cf (MulAdd (SinOsc kr flt-rate (Rand 0 two-pi)) 1400 2000)))
              (RLPF (Mul (LFPulse ar (Lag sq 0.05) 0 pwm) 0.1) cf (FDiv 1 15)))))
       (a (Mul (LFNoise0 ar (MulAdd (LFNoise1 kr 0.3) 6000 8000)) (mce2 0.07 0.08)))
       (x (Mul (Decay (Impulse ar 2 0) 0.15) a))
       (g (Add (mce2 (f 1 8 0.31 0.2) (f 0 2 0.13 0.11)) x))
       (z (Mul 0.4 (Add (CombN g 0.375 0.375 5) (mce-reverse g))))
       (e (env-linen 2 56 2 1 nil)))
  (Mul z (EnvGen kr 1 1 0 1 remove-synth e)))
